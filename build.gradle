plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'maven-publish'
    id 'java'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    group = 'dev.esophose'
    version = '8.0'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        jcenter()

        maven { url = 'https://repo.codemc.org/repository/maven-public' }
        maven { url = 'https://repo.rosewooddev.io/repository/public/' }
        maven { url = 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url = 'https://repo.dmulloy2.net/nexus/repository/public/' }
    }
}

//javadoc {
//    options.encoding = 'UTF-8'
//    if (JavaVersion.current().isJava9Compatible()) {
//        options.addBooleanOption('html5', true)
//    }
//}
//
//java {
//    withJavadocJar()
//    withSourcesJar()
//}

configurations {
    childJars
}

dependencies {
    implementation project(':Plugin')
    implementation project(':NMS-Wrapper')
    implementation project(':NMS-v1_16_R3')

    subprojects.each {
        childJars project(it.path)
    }
}

shadowJar {
    archiveClassifier.set(null)

    relocate('org.bstats', 'dev.esophose.playerparticles.libs.bstats')
    relocate('org.slf4j', 'dev.esophose.playerparticles.libs.slf4j')
    relocate('com.zaxxer.hikari', 'dev.esophose.playerparticles.libs.hikaricp')
    relocate('org.codemc.worldguardwrapper', 'dev.esophose.playerparticles.libs.worldguardwrapper')
}

jar {
    dependsOn configurations.childJars
    from {
        configurations.childJars.collect {
            zipTree(it)
        }
    }
}

build.dependsOn shadowJar
